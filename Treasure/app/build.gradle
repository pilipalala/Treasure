apply plugin: 'com.android.application'
//def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(file("keystore.properties")))
//keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def releaseTime() {
    return new Date().format("MM.dd", TimeZone.getTimeZone("UTC"))
}

def getAppName() {
    return "Treasure"
}

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'
    /**
     * 一、默认产品偏好配置
     */
    defaultConfig {
        applicationId "com.wyj.treasure"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        /* 动态添加 string.xml 字段；
          注意，这里是添加，在 string.xml 不能有这个字段，会重名！！！
          */
        resValue "string", "app_name", getAppName()
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }
    /**
     * 给生成的apk文件重命名，
     */
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${getAppName()}${releaseTime()}.apk"
        }
    }
    //增加java堆内存
    dexOptions {
        javaMaxHeapSize "4g"
    }
    /**
     * 二、自定义签名配置
     */
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        debug {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    /**
     * 三、构建类型，分为release和debug两种
     */
    buildTypes {
        release {
            /*定义是否压缩代码 false表示不压缩*/
            minifyEnabled false
            /*压缩资源文件*/
            shrinkResources false
            /*proguardFiles定义混淆代码的默认混淆规则*/
            /*proguard-android.txt表示系统自带的混淆规则*/
            /*proguard-rules.pro位于当前module根目录下*/
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    sourceSets { main { aidl.srcDirs = ['src/main/aidl', 'src/main/aidl/'] } }


}
repositories {
    flatDir {
        dirs 'lib' //就是你放aar的目录地址
    }
}
/**
 * 引入依赖包的秘密
 */
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:26.0.2'
    implementation 'com.android.support:design:26.0.2'
    implementation 'com.android.support:cardview-v7:26.0.2'
    implementation 'com.android.support:support-vector-drawable:26.0.2'
    implementation 'com.android.support:support-v4:26.0.2'
    //    implementation 'cat.ereza:customactivityoncrash:2.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.jakewharton:butterknife:8.7.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.22'
    implementation 'com.tencent.bugly:crashreport:latest.release'
    implementation 'com.tencent.bugly:nativecrashreport:latest.release'
    implementation 'com.tinkerpatch.sdk:tinkerpatch-android-sdk:1.1.8'
    implementation 'me.grantland:autofittextview:0.2.+'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.0.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.squareup.okhttp3:okhttp:3.4.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    testImplementation 'junit:junit:4.12'
    implementation 'com.github.promeg:tinypinyin:1.0.0'
    implementation 'com.android.support:palette-v7:26.0.2'
    implementation 'jp.wasabeef:glide-transformations:2.0.1'

    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    implementation 'com.google.dagger:dagger:2.5'
    implementation 'com.google.dagger:dagger-compiler:2.5'

    implementation 'com.android.support:multidex:1.0.1'
}
